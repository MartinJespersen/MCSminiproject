class Shift is subclass of Global

instance variables
private id : ID;
private requiredProfessions : map Profession to nat;
private professionsCurrentlyAssigned : map Profession to nat; 

inv dom(professionsCurrentlyAssigned) = dom(requiredProfessions) and
forall prof in set dom(professionsCurrentlyAssigned) & professionsCurrentlyAssigned(prof) <= requiredProfessions(prof);
inv card dom(requiredProfessions) <> 0; 


operations
public Shift : ID * map Profession to nat ==> Shift
Shift(shiftId, reqProf) ==
(
    id := shiftId;
    requiredProfessions := reqProf;
    professionsCurrentlyAssigned := { prof |-> 0 | prof in set dom(reqProf)}
) 
pre dom(reqProf) <> {} and forall i in set rng reqProf & i > 0;



public AddEmployeeToShift : Employee ==> ()
AddEmployeeToShift(employee) ==
(
    def profession = employee.GetProfession()
    in
        professionsCurrentlyAssigned(profession) := professionsCurrentlyAssigned(profession) + 1;
)
pre  employee.GetProfession() in set dom professionsCurrentlyAssigned;


pure public GetAvailableSlots: Employee ==> nat 
GetAvailableSlots(emp) ==
    let prof = emp.GetProfession() in 
        return requiredProfessions(prof) - professionsCurrentlyAssigned(prof)
pre emp.GetProfession() in set dom requiredProfessions and emp.GetProfession() in set dom professionsCurrentlyAssigned
post RESULT <=  requiredProfessions(emp.GetProfession()) and RESULT >= 0;

pure public IsShiftVacant : Profession ==> bool
IsShiftVacant(prof) ==
    return prof in set dom requiredProfessions and 
    professionsCurrentlyAssigned(prof) < requiredProfessions(prof);

public Reset: () ==> () 
Reset() ==
(
    professionsCurrentlyAssigned := {p |-> 0 | p in set dom(professionsCurrentlyAssigned)}
);

pure public GetId : () ==> ID
GetId() == 
    return id; 

end Shift 
